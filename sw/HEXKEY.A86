;F: Mengirimkan kode heksa [Alt-Numpad] dari penekanan 0..9; A..F; a..F
;   pada kbd buffer jika NumLock diaktifkan, dan otomatis mentransposenya
;   jika kode karakter khusus
;	Ctrl-J = BCC
;	Ctrl-K = FrMark (reset BCC)
;	Ctrl-L = BCC+FrMark (reset BCC)
;	Alt-X  = Deinstall
; C: INTEL X86
; R: A18121998

BIOS	SEGMENT	AT 40H

	ORG	17H
KB_SHIFT	LABEL	BYTE
NUMLOCK		EQU	20H

	ORG	1AH
KB_HEAD	LABEL	WORD
	ORG	1CH
KB_TAIL	LABEL	WORD
	ORG	1EH
KB_BUF	LABEL	WORD
	ORG	80H
KB_START	LABEL	WORD
	ORG	82H
KB_END		LABEL	WORD

	BIOS	ENDS

CODE	SEGMENT
ASSUME	CS:CODE,DS:BIOS

	ORG	2CH
PHD_ENV	DW	?

	ORG	100H

START:
	JMP	INSTALL

ORG_INT9	DW	?,?
TEMP_KEY	DB	?
FLAG_PROC	DB	0
BCC_HASIL	DB	0FFH
BCC_AWAL	EQU	0FFH

ID_CHK	EQU	5
PRG_ID:	DB	'ORI'
	DB	00H,00H

	DW	OFFSET PRG_ID
NEW_INT9:
	PUSH	BX
	PUSH	DS
	MOV	BX,40H
	MOV	DS,BX
	MOV	BX,KB_TAIL
	PUSHF
	CALL	DWORD PTR CS:ORG_INT9
	PUSH	AX
	CMP	BX,KB_TAIL
	JNZ	PROCESSKEY
	TEST	CS:FLAG_PROC,1
	JZ	_INT9_IRET
	TEST	KB_SHIFT,NUMLOCK
	JNZ	_INT9_IRET
	CALL	FLUSHTEMPKEY
	JMP	SHORT	_INT9_IRET

PROCESSKEY:
	CALL	READKEY
_INT9_IRET:
	POP	AX
	POP	DS
	POP	BX
	IRET

UNINSTALL:
	XOR	BX,BX
	MOV	DS,BX
	CMP	WORD PTR DS:[9*4],OFFSET NEW_INT9
	JNZ	__RET
	MOV	BX,CS
	CMP	BX,DS:[(9*4)+2]
	JNZ	__RET

	MOV	BX,DX
	MOV	AX,2509H
	LDS	DX,DWORD PTR CS:ORG_INT9
	INT	21H
	MOV	DX,BX

	MOV	BX,ES
	MOV	AX,CS
	MOV	ES,AX
	MOV	AH,49H
	INT	21H
	MOV	ES,BX
	RET

READKEY:
	MOV	AX,[BX]
	MOV	KB_TAIL,BX

	CMP	AX,240AH	; = CTRL-J
	JZ	PUTBCC
	CMP	AX,250BH	; = CTRL-K
	JZ	PUTFRMARK
	CMP	AX,260CH	; = CTRL-L
	JZ	PUTFREND
	CMP	AX,2D00H	; = ALT-X
	JE	UNINSTALL

	TEST	KB_SHIFT,NUMLOCK
	JZ	PUTKEY
	CMP	AL,'a'
	JB	_CekKey
	;CMP	AL,'z'
	;JA	_CekKey
	SUB	AL,'a'-'A'	; AL = UPPER CASE
_CekKey:
	CMP	AL,'J'
	JZ	PUTBCC
	CMP	AL,'K'
	JZ	PUTFRMARK
	CMP	AL,'L'
	JZ	PUTFREND

	SUB	AL,'0'
	JC	__RET
	CMP	AL,9
	JBE	NIBLEKEY
	CMP	AL,'A'-'0'
	JB	__RET
	CMP	AL,'F'-'0'
	JA	__RET
	SUB	AL,'A'-'0'-0AH

ASSUME	DS:CODE

NIBLEKEY:
	MOV	BX,CS
	MOV	DS,BX
	XOR	FLAG_PROC,1
	TEST	FLAG_PROC,1
	JZ	HEXKEY
	MOV	TEMP_KEY,AL
__RET:	RET

HEXKEY:
	MOV	AH,TEMP_KEY
	SHL	AH,1
	SHL	AH,1
	SHL	AH,1
	SHL	AH,1
	OR	AL,AH
	XOR	AH,AH
	JMP	SHORT	PUTKEY

FR_MARK		EQU	0DBH
FR_ESC		EQU	0DCH
TRANSPOSER	EQU	0C7H

ASSUME	DS:BIOS

PUTFREND:
	CALL	CEKTEMPKEY
	CALL	PUTBCC
PUTFRMARK:
	CALL	CEKTEMPKEY
	MOV	AX,FR_MARK
	MOV	CS:BCC_HASIL,BCC_AWAL
	JMP	SHORT	PUTPLAINKEY

PUTBCC:
	CALL	CEKTEMPKEY
	XOR	AH,AH
	MOV	AL,CS:BCC_HASIL
	JMP	SHORT	_PUTNOBCC

CEKTEMPKEY:
	TEST	CS:FLAG_PROC,1
	JZ	_AnyRet

FLUSHTEMPKEY:
	XOR	AX,AX
	XCHG	AL,CS:TEMP_KEY
	AND	CS:FLAG_PROC,NOT 1

PUTKEY:
	XOR	CS:BCC_HASIL,AL
_PUTNOBCC:
	CMP	AL,FR_MARK
	JZ	_PutEscKey
	CMP	AL,FR_ESC
	JZ	_PutEscKey
	CMP	AX,00
	JNZ	PUTPLAINKEY
_PutEscKey:
	PUSH	AX
	MOV	AX,FR_ESC
	CALL	PUTPLAINKEY
	POP	AX
	XOR	AL,TRANSPOSER

PUTPLAINKEY:
	MOV	BX,40H
	MOV	DS,BX
	MOV	BX,KB_TAIL
	MOV	[BX],AX

	ADD	BX,2
	CMP	BX,KB_END
	JNZ	_UpdateTail
	MOV	BX,KB_START

	;SUB	BX,OFFSET KB_BUF-2
	;AND	BX,1EH
	;ADD	BX,OFFSET KB_BUF
_UpdateTail:
	MOV	KB_TAIL,BX
_AnyRet:
	RET

ASSUME	DS:CODE

INSTALL:
	CLD
	MOV	AH,9
	MOV	DX,OFFSET Msg
	INT	21H

	MOV	AX,3509H
	INT	21H
	MOV	DI,ES:[BX-2]
	MOV	SI,OFFSET PRG_ID
	MOV	CX,ID_CHK
	REP	CMPSB
	JZ	CANCEL
	MOV	ORG_INT9,BX
	MOV	ORG_INT9+2,ES

	MOV	DX,OFFSET NEW_INT9
	MOV	AX,2509H
	INT	21H

	MOV	ES,PHD_ENV
	MOV	AH,49H
	INT	21H

	MOV	AH,9
	MOV	DX,OFFSET Msg_Ok
	INT	21H

	MOV	AX,3100H
	MOV	DX,OFFSET INSTALL + 0FH
	MOV	CL,4
	SHR	DX,CL
	INT	21H
	RET

CANCEL:
	MOV	AH,9
	MOV	DX,OFFSET Msg_Abort
	INT	21H
	MOV	AX,4C01H
	INT	21H
	RET

EOL	EQU	0DH,0AH

Msg:	DB	'PACKET HEX KEY, V-98A, (C) Ori Novanda 1998',EOL
	DB	'Guna:',EOL
	DB	'1. Mentransformasikan penekanan tombol 0..9 dan A..F',EOL
	DB	'   kedalam Heksa-Chr (jika NumLock diaktifkan)',EOL
	DB	'2. Mentranspose otomatis karakter khusus (FrMark, FrEsc dan Null)',EOL
	DB	EOL,'Tombol Fungsi:',EOL
	DB	'  NumLock = Toggle proses transformasi',EOL
	DB	'  Ctrl-J  = BCC',EOL
	DB	'  Ctrl-K  = FrMark (reset BCC)',EOL
	DB	'  Ctrl-L  = BCC+FrMark (reset BCC)',EOL
	DB	'  Alt-X   = Deinstall',EOL,EOL,'$'
Msg_Ok:
	DB	'Program diaktifkan',EOL,'$'
Msg_Abort:
	DB	'Program telah aktif',EOL,'$'
	DB	'* I LOVE TIKA *'

	CODE	ENDS
	END	START
